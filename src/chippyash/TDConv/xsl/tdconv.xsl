<?xml version="1.0" encoding="UTF-8"?>

<!--
    Document   : tdconv.xsl
    Created on : 05 December 2014, 07:39
    Author     : Ashley Kitson
    Copyright  : Ashley Kitson, UK, 2014
    License    : GPL V3+
    Description:
        Transform testdox format html file to a markdown format file
        
        Please amend but do not remove copyright, license or "Generated by" stanzas
-->

<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0">
    <xsl:output method="text"/>
    <xsl:param name="title">Foo</xsl:param>
    
    <!-- Overide default template rule - do not show unmatched text -->
    <xsl:template match="text()"/>
    
    <!-- Display title -->
    <xsl:template match="/">
        <xsl:text># </xsl:text><xsl:value-of select="$title"/><xsl:text>&#xa;</xsl:text><xsl:text>&#xa;</xsl:text>
        <xsl:apply-templates/>
        <xsl:text>&#xa;Generated by chippyash/testdox-converter</xsl:text>
    </xsl:template>
    
    <!-- Display each class with its test contracts -->
    <xsl:template match="/html/body/h2">
        <xsl:text>## </xsl:text><xsl:value-of select="."/><xsl:text>&#xa;</xsl:text><xsl:text>&#xa;</xsl:text>
        <xsl:for-each select="./following::ul[1]">
            <xsl:for-each select="li">
                <xsl:text>*  </xsl:text><xsl:value-of select="normalize-space(.)"/><xsl:text>&#xa;</xsl:text>              
            </xsl:for-each>
            <xsl:text>&#xa;</xsl:text>
        </xsl:for-each>
    </xsl:template>
</xsl:stylesheet>
